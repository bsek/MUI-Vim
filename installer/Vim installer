; Vim
; $VER: Vim 8.1

(set @app-name "Vim")
(set @default-dest "RAM:")
(set #root "")

;---------------------------------------------------------------------------------------------------------------------------------------------
; Strings
;---------------------------------------------------------------------------------------------------------------------------------------------
(set #grey_str_c1 "Mason")
(set #grey_str_c2 "Grey")

(if (= @language "svenska")
    (
        (set #welcome_str_p "Installation av Vim")
        (set #destination_str_p "Vänligen välj var Vim ska installeras.\nKatalogen \"Vim\" kommer att skapas där.")
        (set #destination_str_h "")
        (set #copy_str_p "Kopierar program och programdata...")
        (set #toolbar_str_p "Vänligen välj knappradstema")

        (set #assign_str_p "Skapa tilldelning och utöka sökvägen i \"user-startup\"?")
        (set #assign_str_h "")
    )
; else
    (
        (set #welcome_str "Welcome to the Vim installer")
        (set #destination_str_p "Please choose a location for installing Vim.\nA drawer called \"Vim\" will be created there.")
        (set #destination_str_h "")
        (set #copy_str_p "Copying executables and runtime data...")
        (set #toolbar_str_p "Please choose toolbar theme")
        (set #assign_str_p "Create Vim assign and add Vim to path in \"user-startup\"?")
        (set #assign_str_h "")
    )
)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Welcome
;---------------------------------------------------------------------------------------------------------------------------------------------
(welcome #welcome_str_p)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Choose destination
;---------------------------------------------------------------------------------------------------------------------------------------------
(set #dest-root
    (askdir
        (prompt #destination_str_p)
        (help #destination_str_h)
        (default @default-dest)
    )
)
(set @default-dest #dest-root)
(set @default-dest (tackon @default-dest @app-name))
(complete 20)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Copy executables and runtime data. Bail out on skip. No point in continuing.
;---------------------------------------------------------------------------------------------------------------------------------------------
(if (= 0
        (copyfiles
            (source #root)
            (dest @default-dest)
            (prompt #copy_str_p)
            (help @copyfiles-help)
            (infos)
            (all)
            (confirm)
        )
    )
    ; else
    (
        (exit (quiet))
    )
)
(complete 40)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Clean up redundant files
;---------------------------------------------------------------------------------------------------------------------------------------------
(delete (tackon @default-dest "Vim installer") (infos))
(delete (tackon @default-dest "INSTALLATION.txt") (infos))

;---------------------------------------------------------------------------------------------------------------------------------------------
; Select toolbar
;---------------------------------------------------------------------------------------------------------------------------------------------
(set #toolbar
    (askchoice
        (prompt #toolbar_str_p)
        (help @askchoice-help)
        (choices #grey_str_c1 #grey_str_c2)
        (default 0)
    )
)
(complete 60)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Remove default (unpack and run) toolbar
;---------------------------------------------------------------------------------------------------------------------------------------------
(delete
    (tackon @default-dest "runtime/icons/tb_#?strip.png")
    (all)
)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Put the selected toolbar in place
;---------------------------------------------------------------------------------------------------------------------------------------------
(if (<> 0 #toolbar)
    (
        ; Grey
        (delete
            (tackon @default-dest "runtime/icons/tb_#?mason.png")
            (all)
        )
        (rename
            (tackon @default-dest "runtime/icons/tb_strip_grey.png")
            (tackon @default-dest "runtime/icons/tb_strip.png")
        )
        (rename
            (tackon @default-dest "runtime/icons/tb_sel_strip_grey.png")
            (tackon @default-dest "runtime/icons/tb_sel_strip.png")
        )
        (rename
            (tackon @default-dest "runtime/icons/tb_dis_strip_grey.png")
            (tackon @default-dest "runtime/icons/tb_dis_strip.png")
        )
    )
    ; else
    (
        ; Mason
        (delete
            (tackon @default-dest "runtime/icons/tb_#?grey.png")
            (all)
        )
        (rename
            (tackon @default-dest "runtime/icons/tb_strip_mason.png")
            (tackon @default-dest "runtime/icons/tb_strip.png")
        )
        (rename
            (tackon @default-dest "runtime/icons/tb_sel_strip_mason.png")
            (tackon @default-dest "runtime/icons/tb_sel_strip.png")
        )
        (rename
            (tackon @default-dest "runtime/icons/tb_dis_strip_mason.png")
            (tackon @default-dest "runtime/icons/tb_dis_strip.png")
        )

    )
)
(complete 80)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Create Vim assign / add Vim to path in user-startup
;---------------------------------------------------------------------------------------------------------------------------------------------
(set #assign-dir (tackon @default-dest "runtime"))
(startup @app-name
    (prompt #assign_str_p)
    (help #assign_str_h)
    (command ("Assign %s: \"%s\"\nPath ADD >NIL: \"%s\"" @app-name #assign-dir @default-dest))
)
(complete 100)

;---------------------------------------------------------------------------------------------------------------------------------------------
; Run assign to be able to run Vim without a reboot. Add Vim to path (using an assign) as well.
;---------------------------------------------------------------------------------------------------------------------------------------------
(run (cat "assign VIM: " #assign-dir))
(run (cat "assign ADD C: " @default-dest))
